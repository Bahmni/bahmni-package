plugins {
    id "nebula.ospackage" version "3.4.0"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://mavenrepo.openmrs.org/nexus/content/repositories/public'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories'
    }
    mavenLocal()
}

group 'org.bahmni.erp'
version project.bahmniRelease

apply plugin: 'java'

//The openerp-atomfeed-service.war will be copied by the CI server after the artifacts are built.
task extractWar(type: Copy) {
    from zipTree(file("${projectDir}/resources/openerp-atomfeed-service.war"))
    into file("${buildDir}/bahmni-erp-connect")
}

task normalizeJarName(type: Copy) {
    from "${buildDir}" + "/libs/"
    into "${buildDir}" + "/libs/"
    include(project.name + '-' + project.bahmniRelease + '.jar')
    rename(project.name + '-' + project.bahmniRelease + '.jar', project.name + '.jar')
}

ospackage {
    packageName = 'bahmni-erp-connect'
    String initialCounter = System.getenv('INITIAL_PIPELINE_COUNTER') ?: "0"
    String currentCounter=System.getenv('GO_PIPELINE_COUNTER') ?: "1"
    int currentBuildNumber = currentCounter as Integer
    int initialBuildNumber=0;
    if (initialCounter.isInteger()) {
        initialBuildNumber = initialCounter as Integer
    }
    int newBuildNumber = initialBuildNumber + currentBuildNumber
    release = newBuildNumber as String
    arch = NOARCH
    os = LINUX
    user = 'root'

    into '/opt/bahmni-erp-connect'

    from("${projectDir}/resources/log4j.xml") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'bahmni-erp-connect/WEB-INF/classes'
    }

    from("${buildDir}/libs/bahmni-erp-connect.jar") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'lib/'
    }

    from("${projectDir}/resources/") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'etc'
        exclude('run-liquibase.sh')
    }

    from("${projectDir}/resources/run-liquibase.sh") {
        fileMode = 0744
        user 'bahmni'
        into 'etc'
    }

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0744
        createDirectoryEntry = true
        into 'bin'
    }

    from("${projectDir}/resources/placeholder") {
        createDirectoryEntry = true
        into 'run'
    }

    from("${projectDir}/resources/placeholder") {
        createDirectoryEntry = true
        into 'log'
    }

    from("${buildDir}/bahmni-erp-connect/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bahmni-erp-connect'
    }
}

buildRpm {
    dependsOn 'build', 'normalizeJarName', 'extractWar'

    preInstall file("${projectDir}/scripts/preinstall.sh")
    postInstall file("${projectDir}/scripts/postinstall.sh")
    preUninstall file("${projectDir}/scripts/preuninstall.sh")
}

dependencies {
    compile project(":core")
}

jar {
    manifest {
        attributes 'Main-Class': "org.bahmni.deployment.launch.Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
