plugins {
    id "nebula.ospackage" version "3.4.0"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://mavenrepo.openmrs.org/nexus/content/repositories/public'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories'
    }
    mavenLocal()
}

group 'org.bahmni.eventLogService'
version project.bahmniRelease

apply plugin: 'java'

//The event-log-service-webapp.war will be copied by the CI server after the artifacts are built.
task extractWar(type: Copy) {
    from zipTree(file("${projectDir}/resources/event-log-service-webapp.war"))
    into file("${buildDir}/bahmni-event-log-service")
}

task normalizeJarName(type: Copy) {
    from "${buildDir}" + "/libs/"
    into "${buildDir}" + "/libs/"
    include(project.name + '-' + project.bahmniRelease + '.jar')
    rename(project.name + '-' + project.bahmniRelease + '.jar', project.name + '.jar')
}

ospackage {
    packageName = 'bahmni-event-log-service'
    release = System.getenv('GO_PIPELINE_COUNTER')
    arch = NOARCH
    os = LINUX
    user = 'root'

    into '/opt/bahmni-event-log-service'

    from("${projectDir}/resources/log4j.properties") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'bahmni-event-log-service/WEB-INF/classes'
    }

    from("${buildDir}/libs/bahmni-event-log-service.jar") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'lib/'
    }

    from("${projectDir}/resources/") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'etc'
        exclude('run-liquibase.sh')
        exclude('event-log-service-webapp.war')
        exclude('*.checksum')
    }

    from("${projectDir}/resources/run-liquibase.sh") {
        fileMode = 0744
        user 'bahmni'
        into 'etc'
    }

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0744
        createDirectoryEntry = true
        into 'bin'
    }

    from("${projectDir}/scripts/bahmni-event-log-service.service") {
        fileMode = 0744
        user 'root'
        into '/usr/lib/systemd/system/'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0744
        createDirectoryEntry = true
        into 'run'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0644
        createDirectoryEntry = true
        into 'log'
    }


    from("${buildDir}/bahmni-event-log-service/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bahmni-event-log-service'
    }

    from("${projectDir}/resources/") {
        fileMode = 0755
        into 'etc'
        include('bahmni_eventlog_ssl.conf')
    }
}

buildRpm {
    dependsOn 'build','normalizeJarName', 'extractWar'

    preInstall file("${projectDir}/scripts/preinstall.sh")
    postInstall file("${projectDir}/scripts/postinstall.sh")
    preUninstall file("${projectDir}/scripts/preuninstall.sh")
}

dependencies {
    compile project(":core")
}

jar {
    manifest {
        attributes 'Main-Class': "org.bahmni.deployment.launch.Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
